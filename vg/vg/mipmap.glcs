#version 430

layout(local_size_x = 4, local_size_y = 4, local_size_z = 2) in;

layout(std430, binding = 7) buffer densitySSBO
{
	uint density[];
};

uniform uint source_level_start;
uniform uint source_level_resolution;
uniform uint destination_level_start;
uniform uint destination_level_resolution;

void main()
{
		uvec3 destination_position = gl_GlobalInvocationID;

		if(destination_position.x >= destination_level_resolution || destination_position.y >= destination_level_resolution || destination_position.z >= destination_level_resolution) return;

		uint destination_index = destination_position.x + destination_level_resolution * (destination_position.y + destination_level_resolution * destination_position.z);

		uint result = 0;

		for(uint z = 0; z < 2; z++)
		{
			for(uint y = 0; y < 2; y++)
			{
				for(uint x = 0; x < 2; x++)
				{
					uvec3 source_position = destination_position * 2 + uvec3(x, y, z);
					uint source_index = source_position.x + source_level_resolution * (source_position.y + source_level_resolution * source_position.z);

					result += density[source_level_start + source_index];
				}
			}
		}

		result /= 8;

		density[destination_level_start + destination_index] = result;
}
